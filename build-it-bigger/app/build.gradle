apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }

}

android {

    compileSdkVersion 27

    defaultConfig {

        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        buildConfigField("String", "API_URL", API_URL)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    flavorDimensions "tier"

    productFlavors {

        free {
            applicationId "com.udacity.gradle.builditbigger.free"
            dimension "tier"
        }

        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
            dimension "tier"
        }

    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')

    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'

    implementation project(path: ':jokesrepo')
    implementation project(path: ':frontend')

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    freeImplementation 'com.google.android.gms:play-services-ads:11.8.0'

    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation "com.android.support.test.espresso:espresso-idling-resource:3.0.1"
    androidTestImplementation ("com.android.support.test.espresso:espresso-intents:3.0.1")

    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }

}

task startBackend {

    dependsOn ':backend:appengineStart'

    doLast {
        println 'GCE dev server started.'
    }

}

task runInstrumentedTests {

    mustRunAfter 'startBackend'
    dependsOn ':app:connectedAndroidTest'

    doLast {
        println 'Instrumented test ran to completion.'
    }

}

task stopBackend {

    mustRunAfter 'runInstrumentedTests'
    dependsOn ':backend:appengineStop'

    doLast {
        println 'GCE dev server stopped.'
    }

}